=== chapitre_6 ===

= scene_1
~ afficher_fond(transition_6)
<center><titre>Chapitre 6</titre></center><br/><center>Le sens des responsabilités</center> # transition
~afficher_fond(art_house_cafe)
~jouer_musique(theme_general)
~afficher_personnage(JOHN_RANDLE, "slideInLeft")
~afficher_personnage(BONNIE_RANDLE_V2, "slideInRight")
<b>Fred</b> rencontre <b>John</b> et <b>Bonnie</b> au Art House Café.
John: Si ma soeur m’avait parlé de ça plus en détail et plus tôt, je lui aurais conseillé de ne pas vous faire confiance. # character=JOHN_RANDLE expression=sterness

Fred: Alors, je m’estime chanceuse qu’elle ne l’ait pas fait. # character=FRED_GALLANT invisible

Bonnie: Pourquoi vous avez fourni des informations à Christopher? # character=BONNIE_RANDLE_V2 expression=alertness

Fred: Je devais regagner sa confiance, n’est-ce pas? # character=FRED_GALLANT invisible

Bonnie: Oui, mais ce que vous lui avez donné ne l’aide pas dans l’immédiat, il me semble. # character=BONNIE_RANDLE_V2 expression=sterness

Fred: En effet. Mais ça montre ma bonne volonté. # character=FRED_GALLANT invisible

John: Et à nous, vous la montrez quand? # character=JOHN_RANDLE expression=sterness

* <b>Fred</b> est exaspérée par la question. Les nerfs à vif, elle répond un peu froidement.

Fred: Mais bon sang! Je ne vous demande pas grand-chose! Nous travaillons pour la même cause John. Je vous demande un peu de patience. Quel est mon intérêt dans la cause? Je suis agent de police! # character=FRED_GALLANT invisible

John: Je n’ai aucune obligation envers vous! Qu’est-ce que vous voulez à la fin? # character=JOHN_RANDLE expression=wtf

Fred: Mais vous savez très bien que Christopher ne fait rien de très grave. # character=FRED_GALLANT invisible

John: Ça dépend du point de vue. Vendre des jeux à des mineurs, moi je trouve ça grave. Et c’est vous l’agent de police, non? # character=JOHN_RANDLE expression=sterness

Fred: Bon d’accord. Oui, c’est, disons… difficile à justifier. Lorsque je donnerai le dossier à la brigade, il devra payer d’une manière ou d’une autre. Mais si vous m’aidez, je réussirai à faire avancer cette cause qui vous tient à cœur. # character=FRED_GALLANT invisible

John: Votre comportement est très ambivalent. On se demande de quel côté vous êtes. # character=JOHN_RANDLE expression=concern

Fred: Oui, je me le demande tous les jours moi aussi, ces derniers temps. # character=FRED_GALLANT invisible

John: Que voulez-vous dire? # character=JOHN_RANDLE expression=concern

Fred: Rien, rien. # character=FRED_GALLANT invisible

* <b>Fred</b> garde son calme et continue de tenter de convaincre <b>John</b> de l’aider davantage.

Fred: Je comprends votre inquiétude John, mais si vous m’aidez, vous ne le regretterez pas. # character=FRED_GALLANT invisible

John: C’est trop vague comme promesse, je ne suis pas un enfant, moi. # character=JOHN_RANDLE expression=sterness

Fred: Mais vous savez très bien que Christopher ne fait rien de très grave. # character=FRED_GALLANT invisible

John: Ça dépend du point de vue. Vendre des jeux à des mineurs, moi je trouve ça grave. Et c’est vous l’agent de police, non? # character=JOHN_RANDLE expression=sterness

Fred: Bon d’accord. Oui, c’est, disons… difficile à justifier. Lorsque je donnerai le dossier à la brigade, il devra payer d’une manière ou d’une autre. Mais si vous m’aidez, je réussirai à faire avancer cette cause qui vous tient à cœur. # character=FRED_GALLANT invisible

John: Votre comportement est très ambivalent. On se demande de quel côté vous êtes. # character=JOHN_RANDLE expression=concern

Fred: N’est-ce pas le propre d’un agent double? # character=FRED_GALLANT invisible

John: J’ai l’impression qu’il y a plus que deux jeux dans vos démarches. # character=JOHN_RANDLE expression=disdain

Fred: D’accord, écoutez, si je vous disais que parfois, je doute. Je me pose des questions. Je vois des choses qui ne correspondent pas à la version officielle. Je réfléchis. # character=FRED_GALLANT invisible

* <b>Fred</b> craque et supplie <b>John</b> de l’aider.

Fred: Je n’en peux plus! Je vais craquer! # character=FRED_GALLANT invisible

John: Du calme. # character=JOHN_RANDLE expression=concern

Fred: J’en ai marre de supplier tout le monde de m’aider! On dirait que je suis la seule à vouloir que les choses s’améliorent! # character=FRED_GALLANT invisible

John: On veut tous la même chose. Seulement, ce sont vos méthodes qui m’inquiètent. # character=JOHN_RANDLE expression=sterness

Fred: Je croirais entendre mon chef! # character=FRED_GALLANT invisible

John: Peut-être que c’est un signe, non? # character=JOHN_RANDLE expression=sterness

Fred: Mais vous savez très bien que Christopher ne fait rien de très grave. # character=FRED_GALLANT invisible

John: Ça dépend du point de vue. Vendre des jeux à des mineurs, moi je trouve ça grave. Et c’est vous l’agent de police, non? # character=JOHN_RANDLE expression=sterness

Fred: Bon d’accord. Oui, c’est, disons… difficile à justifier. Lorsque je donnerai le dossier à la brigade, il devra payer d’une manière ou d’une autre. Mais si vous m’aidez, je réussirai à faire avancer cette cause qui vous tient à cœur. # character=FRED_GALLANT invisible

John: Votre comportement est très ambivalent. On se demande de quel côté vous êtes. # character=JOHN_RANDLE expression=concern

Fred: Mais si j’abandonne, John. Si j’abandonne, qui va se charger de ce travail? Tous mes collègues sont brûlés, mon chef compte sur moi pour obtenir des résultats. Au point où j’en suis, ça passe ou ça casse. Vous comprenez? # character=FRED_GALLANT invisible

John: Non. # character=JOHN_RANDLE expression=sterness

Fred: Je vous en prie. Ne jouez pas les imbéciles. # character=FRED_GALLANT invisible

- John: Faites attention. Ne vous laissez pas influencer par eux. Ils sont insouciants. Ça ne veut pas dire qu’ils ont raison. # character=JOHN_RANDLE expression=concern

Fred: Je sais. Mais justement, tout n’est pas noir et blanc. Lisez-vous les journaux? On sent qu’il se passe quelque chose, non? # character=FRED_GALLANT invisible

John: Je ne sais pas. Ce que je sais, c’est que je veux que ma soeur sorte de cette organisation et arrête de jouer. Légal ou pas, je préfère ne pas jouer et qu’elle ne joue pas. # character=JOHN_RANDLE expression=melancholy

Fred: C’est votre droit, c’est votre droit. Mais juste un dernier soir? Je n’ai plus de temps, vous comprenez? Je vais agir, très bientôt. # character=FRED_GALLANT invisible

John: Un soir. Je vais prétendre vous connaître, mais je ne jouerai pas. Et si ma soeur est toujours coincée dans une semaine… Comptez les jours, une semaine, je porte plainte contre vous et je dis tout ce que je sais. # character=JOHN_RANDLE expression=sterness

Fred: Entendu. Bonnie, merci. John, à tout à l’heure. # character=FRED_GALLANT invisible
~cacher_tout_personnages()
-> scene_2

=scene_2
~afficher_fond(appartement_christopher)
~jouer_musique(theme_general)
~afficher_personnage(JOHN_RANDLE, "slideInLeft")
~afficher_personnage(CHRISTOPHER_YOUNG, "slideInRight")

<b>Fred</b>, <b>John</b> et <b>Christopher</b> commencent une soirée de tournoi du jeu FIFA 2021.

John: De toute façon, j’ai prévenu Fred que je ne jouais pas, inutile de chercher un autre joueur. # character=JOHN_RANDLE expression=neutral

Christopher: Aaah John! Pourquoi? Ce n’est pas dangereux, nous sommes des adultes. Si tu ne joues pas, ce ne sera pas pareil. # character=CHRISTOPHER_YOUNG expression=amusement

John: C’était ma condition Chris. Je ne reviendrai pas là-dessus. # character=JOHN_RANDLE expression=sterness

Fred: Allez, laisse tomber Christopher. Commençons la partie! # character=FRED_GALLANT invisible

Christopher: Sérieusement, je ne comprends pas! Tu sais Fred, avant, il paraît que c’était épique les tournois d’esport! Tu te rends compte qu’on va le recréer! Il y a encore des pros, bien sûr leur revenu n’est plus le même, les foules non plus. La gloire non plus. # character=CHRISTOPHER_YOUNG expression=wonder

John: Cesse de t’accrocher au passé! Ça ne sera plus jamais comme avant. Les gens ont évolué. Ils n’embarqueront plus dans ces combines. # character=JOHN_RANDLE expression=wtf

Christopher: Ce n’étaient pas des combines et je vais bientôt te le prouver. Fred tu vas m’aider? # character=CHRISTOPHER_YOUNG expression=sterness

Fred: Oui, je crois que mon travail dans l’autre faction est terminé. # character=FRED_GALLANT invisible

Christopher: Oui, tu restes avec nous maintenant. Tu vas m’aider à organiser la démonstration pro sur Only Gamers. # character=CHRISTOPHER_YOUNG expression=neutral

Fred: De quoi auras-tu besoin? # character=FRED_GALLANT invisible

Christopher: Ils disent que tu peux t’occuper des contrats? # character=CHRISTOPHER_YOUNG expression=neutral

Fred: Oui, d’accord. # character=FRED_GALLANT invisible

John: Des contrats! Est-ce qu’on peut appeler cela des contrats quand quelque chose est illégal? # character=JOHN_RANDLE expression=alertness

Fred: J’ai bien peur que oui. # character=FRED_GALLANT invisible

Christopher: Ooooooh! Le tir! T’as vu John! # character=CHRISTOPHER_YOUNG expression=joy

John: J’ai vu! # character=JOHN_RANDLE expression=neutral

Christopher: Tu vas souffrir ce soir Fred! # character=CHRISTOPHER_YOUNG expression=joy

Fred: Oui, je souffre déjà, je dois dire. # character=FRED_GALLANT invisible

Christopher: Hahahaha! Après la partie, je te donne la liste qu’on a. Elle contient les noms de tous les joueurs qui ont le potentiel de devenir professionnels. Ce sont les meilleurs qu’on connaît. Tu vas les contacter? # character=CHRISTOPHER_YOUNG expression=amusement

Fred: Oui, j’imagine que c’est-ce que je ferai d’abord. # character=FRED_GALLANT invisible

Christopher: John? # character=CHRISTOPHER_YOUNG expression=neutral

John: Quoi? # character=JOHN_RANDLE expression=neutral

Christopher: Tu lui donnes le dossier, tu sais, celui sur le esport. # character=CHRISTOPHER_YOUNG expression=neutral

John: Je n’ai plus rien qui concerne les jeux vidéo. # character=JOHN_RANDLE expression=neutral

Christopher: Tu veux rire? Bon, j’ai dû en garder une copie quelque part. # character=CHRISTOPHER_YOUNG expression=wonder

Fred: Qu’est-ce que c’est que ce dossier? # character=FRED_GALLANT invisible

Christopher: C’était notre rêve. Au tout début. Avant même qu’on parle d’un retour légal. On voulait juste recréer l’esport. Comme avant. # character=CHRISTOPHER_YOUNG expression=melancholy

Fred: Ça promet. # character=FRED_GALLANT invisible

John: Ce n’est plus mon rêve. # character=JOHN_RANDLE expression=melancholy

Christopher: Rabat-joie! # character=CHRISTOPHER_YOUNG expression=sterness
~cacher_tout_personnages()

-> quiz_6_1

=quiz_6_1
~ demarrer_quiz(QUIZ_6_1, 3)
~jouer_musique(theme_quiz)
~afficher_document(SPORT_ELECTRONIQUE)
//Description du document : On dirait bien que par le passé, le marché des sports électroniques était très important. Je devrais essayer d’en retenir les éléments principaux si je suis amené à travailler pour la résistance dans ce domaine. <br/> Alors il y avait des joueurs professionnels de jeux vidéo… Voyons voir quel était leur rythme de vie et leur quotidien dans la pratique des sports électroniques.
-> quiz_6_1_question_1

= quiz_6_1_question_1
1- Je me demande à quand remontent les premières compétitions de sport électronique majeures… # question index=1
+   (6_1_1_a) [{6_1_1_a: VISITE--}Les années 80] Les années 80
+   (6_1_1_b) [BON--{6_1_1_b: VISITE--}Les années 90] Les années 90 
Selon les archives, les premières compétitions de sport électronique majeures ont été organisées à la fin des années 1990. Un des premiers jeux vidéo concernés était le jeu de tir à la première personne Quake. # retroaction
-> quiz_6_1_question_2
+   (6_1_1_c) [{6_1_1_c: VISITE--}Les années 2000] Les années 2000
+   (6_1_1_d) [{6_1_1_d: VISITE--}Les années 2010] Les années 2010
- ~ generer_mauvaise_reponse_retroaction()
-> quiz_6_1_question_1

= quiz_6_1_question_2
2- C’était donc des événements très populaires… Je suis curieuse de savoir quel type de public attiraient les compétitions de sport électronique? # question index=2
+   (6_1_2_a) [{6_1_2_a: VISITE--}Majoritairement masculin] Majoritairement masculin
+   (6_1_2_b) [{6_1_2_b: VISITE--}Majoritairement féminin] Majoritairement féminin 
+   (6_1_2_c) [BON--{6_1_2_c: VISITE--}Masculin et féminin] Masculin et féminin
  Selon les archives à ma disposition, le public du sport électronique n’était pas uniquement masculin. Au contraire, une large part du public était féminin. # retroaction
-> quiz_6_1_question_3
- ~ generer_mauvaise_reponse_retroaction()
-> quiz_6_1_question_2

= quiz_6_1_question_3
3- Les compétitions de sports électroniques étaient très peu suivies à travers le monde.# question index=3
+  (6_1_3_a) [{6_1_3_a: VISITE--}Vrai] Vrai
+  (6_1_3_b) [BON--{6_1_3_b: VISITE--}Faux] Faux
 Bien au contraire, les compétitions de sports électroniques étaient très populaires à travers le monde. Les chiffres ont régulièrement augmenté d’année en année et ont même atteint plusieurs millions de spectateurs. # retroaction
-> quiz_6_1_fin
- ~ generer_mauvaise_reponse_retroaction()
-> quiz_6_1_question_3

= quiz_6_1_fin
~terminer_quiz()
-> scene_3

=scene_3
~afficher_fond(art_house_cafe)
~jouer_musique(theme_general)
~afficher_personnage(TREVOR, "slideInUp")
~afficher_personnage(BRAD, "slideInUp")
~afficher_personnage(ANDREA, "slideInUp")
~afficher_personnage(JEN, "slideInUp")

<b>Fred</b> rencontre de potentiels joueurs d’esport; <b>TREVOR</b>, <b>BRAD</b>, <b>ANDREA</b> et <b>JEN</b>.

Fred: Merci beaucoup d’être venus. Je suis très heureuse de faire votre connaissance. # character=FRED_GALLANT invisible

Trevor: Tout le plaisir est pour nous! # character=TREVOR expression=satisfaction
~ajouter_contact(TREVOR)

Fred: Vous savez pourquoi je vous ai convoqués? # character=FRED_GALLANT invisible

Jen: C’est vrai ces rumeurs sur le retour des jeux vidéo? # character=JEN expression=wonder
~ajouter_contact(JEN)

Fred: Je ne sais pas à quoi vous faites référence exactement, mais il y a des groupes de personnes qui essaient de concrétiser ce projet, en effet. # character=FRED_GALLANT invisible

Andrea: Ce sont eux qui veulent nous engager? # character=ANDREA expression=wonder
~ajouter_contact(ANDREA)

Fred: Peu importe. Ce qui m’intéresse c’est d’abord de comprendre ce qu’est la pratique compétitive. # character=FRED_GALLANT invisible

Brad: C’est le bonheur! # character=BRAD expression=joy
~ajouter_contact(BRAD)

Fred: Mais encore? J’avoue ne pas comprendre l’engouement. D’abord, l’idée de regarder des gens jouer me semble bizarre. Ensuite, vous, que retirez-vous en tant que joueurs professionnels? Il me semble que ça n’est pas sérieux et j’ai du mal à imaginer ce que ça pouvait être avant la loi. # character=FRED_GALLANT invisible

Brad: Ne nous insulte pas! # character=BRAD expression=wtf

Trevor: Qui es-tu d’abord? Pourquoi ils nous ont envoyé quelqu’un qui ne sait pas de quoi il parle? # character=TREVOR expression=disdain
~jouer_musique(theme_choix_cruciaux)
<b>Fred</b> doit réagir.

* <b>Fred</b> tente de sauver la situation tant bien que mal.
~points_fin_B1 += 4
~jouer_musique(theme_general)

Fred: Je sais de quoi je parle quand il s’agit de la loi. J’avoue que je suis un peu nul en jeu professionnel, mais j’apprends vite. Voulez-vous m’expliquer un peu ce qu’il en est? # character=FRED_GALLANT invisible

Jen: Tu dois comprendre ce que c’est. Tu dois le sentir. Tu dois sentir l’excitation qui monte lorsque les joueurs pros battent des records, gagnent, font des manœuvres impossibles! # character=JEN expression=wonder

* <b>Fred</b> s’excuse.
~points_fin_B1 += 1
~jouer_musique(theme_general)

Fred: Il faut me pardonner. Je n’ai jamais vu de joueurs professionnels à l’œuvre, mais si je viens vers vous, c’est parce que ça m’intéresse. Voulez-vous m’initier? # character=FRED_GALLANT invisible

Andrea: T’as pas mal de chemin à faire. Mais la première chose que tu dois comprendre, c’est que ce n’est plus un jeu à notre niveau. C’est de l’art. # character=ANDREA expression=neutral

* <b>Fred</b> continue de dire qu’elle n’y comprend rien, mais qu’elle est là pour apprendre.
~points_fin_B2 += 4
~jouer_musique(theme_general)

Fred: Ne soyez pas susceptibles! C’est vrai, je ne comprends pas. Mais je suis là pour apprendre. Allez-vous m’aider à comprendre? # character=FRED_GALLANT invisible

Trevor: Il y a des gens qui ne sont tout simplement pas faits pour comprendre. # character=TREVOR expression=disdain

Fred: Bon. Je vois que nous sommes partis sur de mauvaises bases. Comment puis-je vous aider si vous ne m’expliquez pas ce que vous faites? # character=FRED_GALLANT invisible

Brad: On n’aime pas être traités en paria. # character=BRAD expression=sterness

Fred: Mais non. Mais non. Qu’est-ce qui vous intéresse dans le jeu pour aller jusqu’à en faire un métier? # character=FRED_GALLANT invisible

Andrea: C’est pas un métier. C’est plus du jeu non plus. C’est un art. # character=ANDREA expression=wonder


- Fred: Alors? Comment ça se passe quand vous jouez? Qui vous regarde? # character=FRED_GALLANT invisible

Brad: Tous ceux qui aiment jouer. Tous ceux qui comprennent les difficultés, le contrôle que ça demande, les réflexes. # character=BRAD expression=neutral

Trevor: Hey l’apprenti! Savais-tu que les comparaisons avec le sport ne sont pas gratuites? On est vraiment des athlètes. # character=TREVOR expression=sterness

Fred: Vraiment? # character=FRED_GALLANT invisible

Jen: Oui, vraiment! À 30 ans, on est fini. Nos réflexes ne sont plus assez aiguisés. On parle de fractions de seconde au niveau pro, tu saisis? # character=JEN expression=melancholy

Fred: C’est sérieux. Vous avez des records? # character=FRED_GALLANT invisible

Andrea: Bien sûr! # character=ANDREA expression=wonder

Fred: Mais qu’est-ce qui vous distingue des autres joueurs? Le talent en jeux vidéo c’est quoi? # character=FRED_GALLANT invisible

Trevor: Ça peut être un style distinctif. Une marque de commerce. # character=TREVOR expression=neutral

Brad: Une façon de jouer qu’on reconnaît. # character=BRAD expression=neutral

Andrea: Une signature. # character=ANDREA expression=neutral

Fred: Oui, je comprends. # character=FRED_GALLANT invisible

Trevor: KennyS était le meilleur snipper au monde sur CS. # character=TREVOR expression=satisfaction

Brad: Shroud voyait toujours les ennemis le premier. # character=BRAD expression=joy 

Andrea: Aydan sur Fortnite avait un style agressif, il battait tout le monde sur tous les plans. # character=ANDREA expression=joy

Fred: Mais qui regardait ces joueurs? # character=FRED_GALLANT invisible

Brad: Tous ceux qui jouaient et même ceux qui ne jouaient pas. Ces athlètes étaient aussi des mentors, ils donnaient des trucs, répondaient aux questions des plus jeunes. Ils avaient une bonne attitude, tu vois. Ils donnaient l’exemple. # character=BRAD expression=satisfaction

Fred: Mais il devait y en avoir qui n’avait pas une bonne attitude? # character=FRED_GALLANT invisible

Jen: Une minorité, peut-être! # character=JEN expression=wonder

Fred: Et vous? Êtes-vous capables d’inspirer la jeunesse? Serez-vous les premiers à reprendre une carrière professionnelle de jeux vidéo? # character=FRED_GALLANT invisible

Brad: Quel est le jeu qu’ils veulent mettre sur leur plateforme? # character=BRAD expression=neutral

Fred: C’est un nouveau jeu. # character=FRED_GALLANT invisible

Trevor: Donnez-nous le prototype. Dans une semaine, on le maîtrise. # character=TREVOR expression=satisfaction
~cacher_tout_personnages()
-> scene_4

=scene_4
~afficher_fond(bureau_fred)
~jouer_musique(theme_general)

<b>Fred</b> se rappelle de l’affaire Morris non résolue dont lui avaient parlé les agents <b>Moore</b> et <b>Lucas</b> il y a quelque temps déjà… Elle décide de consulter le dossier.

Fred, à elle même: L’affaire Morris remonte à quelques années et les allégations portent sur un joueur, Eddie Morris, l’un des derniers joueurs professionnels… # character=FRED_GALLANT invisible

Fred, à elle même: Ce dernier a fait une importante dépression et c’est pendant une crise majeure qu’il a été arrêté pour avoir troublé la paix. Il se trouvait au 530 rue Mcleod. À l’époque, on ignorait que le grenier abritait des joueurs et on n’avait arrêté personne d’autre que Morris. Cependant, depuis, les soupçons planaient sur la maison. # character=FRED_GALLANT invisible

Fred, à elle même: Il y a encore les documents saisis à l’époque dans le dossier. # character=FRED_GALLANT invisible
~ajouter_document(PRATIQUE_SPORT_ELECTRONIQUE)
Fred, à elle même: Il semble que les informations sur l’esport ne soient pas aussi roses que celles partagées par les passionnés… Allons explorer tout ça. # character=FRED_GALLANT invisible
-> quiz_6_2

= quiz_6_2
~ demarrer_quiz(QUIZ_6_2, 3)
~jouer_musique(theme_quiz)
~afficher_document(PRATIQUE_SPORT_ELECTRONIQUE)
-> quiz_6_2_question_1

= quiz_6_2_question_1
1- Il semblait que les athlètes professionnels de sport électronique s'entrainaient jusqu'à 12 heures par jour… C’était vraiment possible? # question index=1
+  (6_2_1_a) [BON--{6_2_1_a: VISITE--}Oui] Oui
 Selon les témoignages d’époque, les joueurs s’entrainaient à la manière d’athlètes professionnels, et ce jusqu’à 12 heures par jour. # retroaction
-> quiz_6_2_question_2
+  (6_2_1_b) [{6_2_1_b: VISITE--}Non] Non
- ~ generer_mauvaise_reponse_retroaction()
-> quiz_6_2_question_1

= quiz_6_2_question_2
2- À coup de 12 heures par jour, la carrière d’un joueur ne devait pas être très longue… Avec ce rythme, peu de chances de durer plus de 10 ans. # question index=2
+  (6_2_2_a) [BON--{6_2_2_a: VISITE--}Vrai] Vrai
Selon les archives, les carrières au plus haut niveau dans le domaine du sport électronique étaient relativement courtes. Les joueurs pouvaient souffrir de douleurs chroniques et d’épuisement. Il semble néanmoins que ça commençait à évoluer juste avant l'interdiction. # retroaction
-> quiz_6_2_question_3
+  (6_2_2_b) [{6_2_2_b: VISITE--}Faux] Faux
- ~ generer_mauvaise_reponse_retroaction()
-> quiz_6_2_question_2

= quiz_6_2_question_3
3- Sur ces courtes carrières, je me demande si les joueurs professionnels pouvaient gagner leur vie en jouant. # question index=3
+  (6_2_3_a) [BON--{6_2_3_a: VISITE--}Oui] Oui
Selon les informations disponibles sur le passé, les joueurs professionnels pouvaient très bien gagner leur vie en jouant pour des équipes professionnelles, mais aussi grâce aux commanditaires. Pour la ligue nord-américaine de League of Legends, on parlait de 300 000 dollars en moyenne par an. Il y avait également des salaires minimums prévus par les règlements. # retroaction
-> quiz_6_2_fin
+  (6_2_3_b) [{6_2_3_b: VISITE--}Non] Non
- ~ generer_mauvaise_reponse_retroaction()
-> quiz_6_2_question_3

= quiz_6_2_fin
~terminer_quiz()
-> scene_5

= scene_5
~afficher_fond(rue_mcleod)
~jouer_musique(theme_general)
~afficher_personnage(CHRISTOPHER_YOUNG, "slideInUp")
<b>Fred</b> et <b>Christopher</b> discutent.

Christopher: Alors, tu les as trouvés comment? # character=CHRISTOPHER_YOUNG expression=wonder

Fred: Très bien, très gentils. # character=FRED_GALLANT invisible

Christopher: On les engage? # character=CHRISTOPHER_YOUNG expression=neutral

Fred: J’imagine. Mais il va en falloir d’autres. # character=FRED_GALLANT invisible

Christopher: Oui, je sais, ce n’est qu’un début. J’ai une liste à te donner. Mais à eux, je leur fais parvenir le jeu? # character=CHRISTOPHER_YOUNG expression=neutral

Fred: D’accord. # character=FRED_GALLANT invisible

Christopher: Tu veux jeter un coup d’œil à leur contrat? J’ai reçu la copie de la part de l’autre faction, mais je n’y connais rien. J’aimerais que tu vérifies s’il y a des clauses qui ne devraient pas y être ou s’il en manque! # character=CHRISTOPHER_YOUNG expression=concern

Fred: D’accord, laisse-moi une copie, je regarde cela à tête reposée. # character=FRED_GALLANT invisible
~ajouter_document(CONTRAT_ESPORT)
~cacher_tout_personnages()
~ afficher_fond(blackout)
~jouer_musique(theme_narrateur)

Narrateur: Voilà qu’on sollicite à nouveau votre aide. Saurez-vous bien comprendre les enjeux? Attention aux dérives… # character=NARRATEUR invisible
-> quiz_6_3

=quiz_6_3
~ demarrer_quiz(QUIZ_6_3, 7)
~jouer_musique(theme_quiz)
~afficher_document(CONTRAT_ESPORT)
-> quiz_6_3_question_1

= quiz_6_3_question_1
1- Voyons voir ce que le contrat prévoit en matière de pari : # question index=1
+   (6_3_1_a) [BON--{6_3_1_a: VISITE--}Les joueurs n’ont pas le droit de parier.] Les joueurs n’ont pas le droit de parier. 
Le contrat est très restrictif. Les joueurs ne peuvent pas s’adonner aux paris, peu importe le domaine (voir l’article 2). # retroaction
-> quiz_6_3_question_2
+   (6_3_1_b) [{6_3_1_b: VISITE--}Les joueurs n’ont pas le droit de parier sur le sport électronique.] Les joueurs n’ont pas le droit de parier sur le sport électronique.
+   (6_3_1_c) [{6_3_1_c: VISITE--}Les joueurs peuvent parier dans n’importe quel domaine.] Les joueurs peuvent parier dans n’importe quel domaine.
+   (6_3_1_d) [{6_3_1_d: VISITE--}Le contrat ne prévoit rien en matière de pari.] Le contrat ne prévoit rien en matière de pari. 
- ~ generer_mauvaise_reponse_retroaction()
-> quiz_6_3_question_1

= quiz_6_3_question_2
2- Qu’est-il prévu en ce qui concerne les gains lors des tournois? # question index=2
+   (6_3_2_a) [{6_3_2_a: VISITE--}Les joueurs reçoivent 70 % des gains.] Les joueurs reçoivent 70 % des gains.
+   (6_3_2_b) [{6_3_2_b: VISITE--}Les joueurs ne reçoivent aucune part de ces gains.] Les joueurs ne reçoivent aucune part de ces gains.
+   (6_3_2_c) [BON--{6_3_2_c: VISITE--}Les joueurs doivent partager leurs gains avec leurs employeurs.] Les joueurs doivent partager leurs gains avec leurs employeurs.
Selon le contrat, les joueurs ne recevront pas l'entièreté des gains obtenus en tournoi (voir l’article 5). Est-ce équitable? Il ne recevront même pas 50 % des gains totaux. # retroaction
-> quiz_6_3_question_3
+   (6_3_2_d) [{6_3_2_d: VISITE--}Le contrat est silencieux dans ce domaine.] Le contrat est silencieux dans ce domaine. 
- ~ generer_mauvaise_reponse_retroaction()
-> quiz_6_3_question_2

= quiz_6_3_question_3
3- Je me demande quel est le degré de liberté des joueurs qui voudraient quitter l’équipe avant la fin de leur contrat : # question index=3
+   (6_3_3_a) [{6_3_3_a: VISITE--}Les joueurs peuvent quitter l’équipe à tout moment sans formalités.] Les joueurs peuvent quitter l’équipe à tout moment sans formalités.
+   (6_3_3_b) [BON--{6_3_3_b: VISITE--}Les joueurs doivent négocier leur départ et une compensation devra être payée.] Les joueurs doivent négocier leur départ et une compensation devra être payée.
 Les joueurs ne peuvent pas quitter l’équipe avant la fin de leur contrat sans payer un « buy out » ou une compensation (voir l’article 3). Ils n’ont donc pas une grande liberté de mouvement. # retroaction
-> quiz_6_3_question_4
+   (6_3_3_c) [{6_3_3_c: VISITE--}Les joueurs ne peuvent pas quitter l’équipe sans l’accord de leur employeur.] Les joueurs ne peuvent pas quitter l’équipe sans l’accord de leur employeur.
+   (6_3_3_d) [{6_3_3_d: VISITE--}Ce thème n’est pas traité dans le contrat.] Ce thème n’est pas traité dans le contrat. 
- ~ generer_mauvaise_reponse_retroaction()
-> quiz_6_3_question_3

= quiz_6_3_question_4
4- Existe-t-il des clauses de rupture du contrat? # question index=4
+   (6_3_4_a) [{6_3_4_a: VISITE--}Le thème n’est pas traité dans le contrat.] Le thème n’est pas traité dans le contrat.
+   (6_3_4_b) [{6_3_4_b: VISITE--}L’équipe peut mettre fin au contrat pour n’importe quelle raison avec un préavis de 3 mois.] L’équipe peut mettre fin au contrat pour n’importe quelle raison avec un préavis de 3 mois. 
+   (6_3_4_c) [{6_3_4_c: VISITE--}L’équipe ne peut pas mettre fin au contrat sauf avec l’accord du joueur.] L’équipe ne peut pas mettre fin au contrat sauf avec l’accord du joueur.
+   (6_3_4_d) [BON--{6_3_4_d: VISITE--}L’équipe peut mettre fin très rapidement au contrat pour plusieurs raisons.] L’équipe peut mettre fin très rapidement au contrat pour plusieurs raisons.
L’équipe peut mettre fin au contrat d’un joueur avec un préavis de 3 jours (voir l’article 6). Les motifs qui peuvent être utilisés sont très larges, par exemple de mauvaises performances. Ce n’est pas une clause très protectrice des joueurs. # retroaction
-> quiz_6_3_question_5
- ~ generer_mauvaise_reponse_retroaction()
-> quiz_6_3_question_4

= quiz_6_3_question_5
5- Les joueurs ont-ils des droits en ce qui concerne le choix des compétitions? # question index=5
+   (6_3_5_a) [BON--{6_3_5_a: VISITE--}Les joueurs doivent avoir l’accord de l’équipe pour participer à un tournoi.] Les joueurs doivent avoir l’accord de l’équipe pour participer à un tournoi. 
L’accord de l’équipe est nécessaire pour qu’un joueur puisse participer à un tournoi. Où est la liberté des joueurs dans tout cela? Ils n’ont pas le droit de joueur un certain nombre de tournois à leur guise durant l’année. # retroaction
-> quiz_6_3_question_6
+   (6_3_5_b) [{6_3_5_b: VISITE--}Le thème n’est pas traité dans le contrat.] Le thème n’est pas traité dans le contrat. 
+   (6_3_5_c) [{6_3_5_c: VISITE--}Les joueurs peuvent participer à tous les tournois sans permission.] Les joueurs peuvent participer à tous les tournois sans permission.
+   (6_3_5_d) [{6_3_5_d: VISITE--}Les joueurs peuvent participer à 3 tournois en dehors des tournois officiels.] Les joueurs peuvent participer à 3 tournois en dehors des tournois officiels. 
- ~ generer_mauvaise_reponse_retroaction()
-> quiz_6_3_question_5

= quiz_6_3_question_6
6- Qu’en est-t-il du droit à l’image pour les joueurs? # question index=6
+   (6_3_6_a) [BON--{6_3_6_a: VISITE--}L’équipe peut utiliser l’image des joueurs de différentes manières sans avoir à les payer.] L’équipe peut utiliser l’image des joueurs de différentes manières sans avoir à les payer.
L’équipe peut utiliser à sa guise l’image des joueurs pour plusieurs raisons : la promotion générale de l’équipe (voir l’article 11) ou encore la création de produits dérivés (voir l’article 13). Les joueurs peuvent également être appelés pour participer à des événements de relations publiques, et ce sans contrepartie financière supplémentaire. # retroaction
-> quiz_6_3_question_7
+   (6_3_6_b) [{6_3_6_b: VISITE--}L’équipe doit payer les joueurs en plus de leur salaire pour l’utilisation de leur image.] L’équipe doit payer les joueurs en plus de leur salaire pour l’utilisation de leur image.
+   (6_3_6_c) [{6_3_6_c: VISITE--}Le thème n’est pas traité dans le contrat.] Le thème n’est pas traité dans le contrat.
+   (6_3_6_d) [{6_3_6_d: VISITE--}Le joueur doit autoriser l’usage de son image au cas par cas.] Le joueur doit autoriser l’usage de son image au cas par cas. 
- ~ generer_mauvaise_reponse_retroaction()
-> quiz_6_3_question_6

= quiz_6_3_question_7
7- On parle de sport, alors voyons voir ce que le contrat prévoit à propos de l’usage des produits dopants. # question index=7
+   (6_3_7_a) [{6_3_7_a: VISITE--}L’usage des produits dopants est toléré en 2047.] L’usage des produits dopants est toléré en 2047.
+   (6_3_7_b) [{6_3_7_b: VISITE--}L’usage des produits dopants est interdit.] L’usage des produits dopants est interdit.
+   (6_3_7_c) [BON--{6_3_7_c: VISITE--}Le thème n’est pas traité dans le contrat.] Le thème n’est pas traité dans le contrat.
Le thème n’est pas traité dans ce contrat. Les joueurs peuvent néanmoins être expulsés de l’équipe pour comportement non professionnel. Le dopage pourrait peut-être être un comportement de ce type. # retroaction
-> quiz_6_3_fin
+   (6_3_7_d) [{6_3_7_d: VISITE--}L’usage des produits dopants est interdit et les joueurs doivent se soumettre à des tests réguliers.] L’usage des produits dopants est interdit et les joueurs doivent se soumettre à des tests réguliers. 
- ~ generer_mauvaise_reponse_retroaction()
-> quiz_6_3_question_7

=quiz_6_3_fin
~terminer_quiz()
-> scene_6

= scene_6
~afficher_fond(rue_mcleod)
~jouer_musique(theme_choix_cruciaux)
<b>Fred</b> doit décider si elle va parler des clauses potentiellement abusives à Christopher. # center
* <b>Fred</b> décide de ne pas parler des clauses potentiellement abusives à <b>Christopher</b>.
~points_fin_B2 += 4
~jouer_musique(theme_general)
~afficher_personnage(CHRISTOPHER_YOUNG, "slideInUp")

Christopher: Alors Fred, ce contrat, qu’en penses-tu? On peut aller de l’avant? # character=CHRISTOPHER_YOUNG expression=concern

Fred: J’ai relu tout ça en détail et pour moi tout est bon. La résistance, comme les joueurs, sont très bien protégés. Je ne vois pas ce qui pourrait déraper. # character=FRED_GALLANT invisible

Christopher: Formidable! Merci Fred pour ce coup de main dans l’urgence. Nous préférons ne rien laisser au hasard. # character=CHRISTOPHER_YOUNG expression=satisfaction

* <b>Fred</b> décide d’avertir <b>Christopher</b> de la présence de clauses potentiellement abusives dans le contrat.
~points_fin_B1 += 4
~jouer_musique(theme_general)
~afficher_personnage(CHRISTOPHER_YOUNG, "slideInUp")

Christopher: Alors Fred, ce contrat, qu’en penses-tu? On peut aller de l’avant? # character=CHRISTOPHER_YOUNG expression=concern

Fred: J’ai relu tout ça en détail et j’ai trouvé des choses très problématiques. # character=FRED_GALLANT invisible

Christopher: Comment ça? # character=CHRISTOPHER_YOUNG expression=concern

Fred: Votre contrat contient possiblement des clauses abusives. Les droits des joueurs ne sont pas bien protégés et je peux imaginer des tensions et des conflits potentiels avec l’équipe. # character=FRED_GALLANT invisible

Christopher: Mais ça ne va pas du tout ça! On ne peut se permettre aucun accroc avec les joueurs. Cela fait trop longtemps que nous nous préparons. Nous ne pouvons pas risquer un scandale avec les joueurs. Qu’est-ce que tu recommandes? # character=CHRISTOPHER_YOUNG expression=wonder

Fred: Il faut réécrire en partie le contrat et rééquilibrer les relations entre les joueurs et l’équipe. Pour le moment la balance penche trop en faveur de l’équipe. # character=FRED_GALLANT invisible

Christopher: Merci pour ta vigilance et tes conseils Fred, je remets l’équipe là-dessus immédiatement. # character=CHRISTOPHER_YOUNG expression=satisfaction
- ~cacher_tout_personnages()

 ~afficher_fond(maison_fred)
 De retour chez elle, <b>Fred</b> profite d’un moment de répit pour réfléchir à ce qu’elle va rapporter au <b>Chef Miller</b>.
~jouer_musique(theme_choix_cruciaux)

* <b>Fred</b> envoie des messages textes au chef <b>Miller</b> pour le mettre au courant de la tentative de la résistance de remettre des équipes d’esport sur la carte. 
~points_fin_A1 += 4
~jouer_musique(theme_general)
~afficher_personnage(CHEF_MILLER, "fadeIn")
Fred: Chef, j’ai obtenu des informations intéressantes. Pour l’événement, ils veulent ressusciter l’esport. Je vous tiens au courant à mesure que les choses se précisent. # character=FRED_GALLANT invisible

Fred: Je suis responsable de créer une équipe de pros. Ces noms devraient nous mener à plusieurs joueurs très impliqués. # character=FRED_GALLANT invisible

Chef Miller: Bon boulot, Gallant. # character=CHEF_MILLER expression=satisfaction

~cacher_tout_personnages()

* <b>Fred</b> envoie une fausse piste au chef <b>Miller</b> dans le but de ralentir l’opération qui vise à court-circuiter le retour des jeux.
~points_fin_A2 += 4
~jouer_musique(theme_general)
~afficher_personnage(CHEF_MILLER, "fadeIn")
Fred: Chef, j’ai obtenu des informations intéressantes. La résistance est dans l’erreur. # character=FRED_GALLANT invisible

Fred: Ils ne s’occupent pas des questions qui influenceraient beaucoup l’opinion publique. # character=FRED_GALLANT invisible

Fred: Je fais de mon mieux pour mal les conseiller. # character=FRED_GALLANT invisible

Chef Miller: Bon boulot, Gallant. <br/> Soyez prudente. # character=CHEF_MILLER expression=concern

~cacher_tout_personnages()

* <b>Fred</b> n’envoie rien au chef <b>Miller</b> tant qu’il ne demande rien.
~points_fin_A2 += 2

*<b>Fred</b> envoie une partie de la vérité au chef <b>Miller</b>, mais cache le plus important. 
~points_fin_A1 += 2
~jouer_musique(theme_general)
~afficher_personnage(CHEF_MILLER, "fadeIn")
Fred: Chef Miller, je suis mieux placée dans la résistance. Je vais avoir accès d’ici peu à une liste de joueurs qui ont le potentiel de mener à d’autres réseaux. # character=FRED_GALLANT invisible

Chef Miller: Quelle liste? # character=CHEF_MILLER expression=neutral

Fred: Des joueurs qui sont connus dans la sous-culture. # character=FRED_GALLANT invisible

Chef Miller: Sont-ils liés au 530? # character=CHEF_MILLER expression=wonder

Fred: Non. Ce sont de nouvelles pistes. # character=FRED_GALLANT invisible

Chef Miller: Bon boulot, Gallant. # character=CHEF_MILLER expression=satisfaction

~cacher_tout_personnages()

- ~afficher_fond(news_room_chapitre_6)
~jouer_musique(theme_news)
Quel avenir pour les centres de traitement des dépendances aux jeux? La politique de l’autruche
~arreter_musique()
-> chapitre_7.scene_1